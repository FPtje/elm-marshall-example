{ fetchgit        ? (import <nixpkgs> {}).fetchgit
}:
let
  # Pin down the nixpkgs.
  pkgs = (import (fetchgit {
      url = "https://github.com/FPtje/nixpkgs.git";
      rev = "158b4c8b9c84d43f467ca14c42754a3d2b1f5245";
      sha256 = "0c1gvcxqayvhh34z5d3rl5cv147sj0yin32xrjaga85amz67i44r";
    }) {}).pkgs;

  ghcjsCallPackage = pkgs.haskell.packages.ghcjsHEAD.callPackage;

  # elm-marshall library, used to do the actual marshalling
  # elm-marshall-src = ../elm-marshall; # For developing
  elm-marshall-src = fetchgit {
    url = https://github.com/FPtje/elm-marshall.git;
    rev = "72f5d66792dd5a69d8578d4f1b614fbc34c1a876";
    sha256 = "08lb26kdqpig2ga8nwxliwp6999jfncsykfqhfvmg62vqk2frgh7";
  };
  elm-marshall = ghcjsCallPackage elm-marshall-src { };


  # Elm-export library is used to generate Elm types from Haskell types
  elm-export-src = ../elm-export;
  elm-export = pkgs.haskellPackages.callPackage elm-export-src {};
  elm-export-ghcjs = ghcjsCallPackage elm-export-src {};


  # The haskell package that generates the common types
  common = pkgs.callPackage ./common { inherit elm-export; };

  # The Elm file generated by common
  common-elm = pkgs.runCommand "Types.elm" {} ''
    ${common}/bin/common
    cp Types.elm $out
  '';

  # ghcjs compiled common library (the other one is plain ghc)
  # This makes the common types available to ghcjs
  common-ghcjs = ghcjsCallPackage ./common { compiler = "ghcjsHEAD"; elm-export = elm-export-ghcjs; inherit elm-marshall; };

  # The elm side of the program
  elm-side = pkgs.callPackage ./elm { inherit common-elm; };

  # The ghcjs side of the program
  ghcjs-side = ghcjsCallPackage ./ghcjs { inherit common-ghcjs elm-marshall; };

  # The final derivation
  drv = pkgs.stdenv.mkDerivation {
    name = "elm-marshall-helloworld";

    phases = [ "buildPhase" ];
    src = ./.;

    # Home is set because of this bug:
    # https://github.com/elm-lang/elm-make/issues/93
    buildPhase = ''
      mkdir $out

      cp ${elm-side}/main.js $out/
      cp -r ${./static}/* $out/
      cp ${ghcjs-side}/bin/elm-marshall.jsexe/all.js $out/ghcjs.js
    '';
  };
in
drv
